apply plugin: "com.android.application"

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    buildFeatures {
        prefab true
    }

    namespace "ai.connct_screen.rn"
    defaultConfig {
        applicationId "ai.connct_screen.rn"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('connect-screen.jks')
            storePassword 'Iam20Now'
            keyAlias 'connect-screen'
            keyPassword 'Iam20Now'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

// Copy llm-config.json into assets so it's bundled in the APK (optional)
task copyLlmConfig {
    doLast {
        def src = file("${rootProject.projectDir}/llm-config.json")
        if (src.exists()) {
            def dest = file("${projectDir}/src/main/assets/llm-config.json")
            dest.parentFile.mkdirs()
            dest.text = src.text
        }
    }
}
preBuild.dependsOn copyLlmConfig

// Build standalone agent JS (esbuild: TS â†’ JS into assets)
task buildAgentJs(type: Exec) {
    def projectRoot = file("../../")
    workingDir projectRoot
    commandLine "npx", "esbuild",
        "src/agent-standalone.ts",
        "--bundle", "--format=iife", "--target=es2020",
        "--outfile=android/app/src/main/assets/agent-standalone.js"
}
preBuild.dependsOn buildAgentJs

dependencies {
    // Hermes engine (provides hermes-engine::hermesvm cmake target)
    implementation("com.facebook.hermes:hermes-android:250829098.0.7")

    // OkHttp for Java-level WebSocket (DeviceConnection)
    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    // AppCompat for AppCompatActivity
    implementation("androidx.appcompat:appcompat:1.6.1")
}
